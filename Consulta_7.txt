#Consulta 7. Parcelas Irregulares en Asturias (33) (con más de 2 CD distintas)

SELECT DISTINCT ?plot3 ?lat ?lng COUNT(?DC) AS ?nDC
WHERE {
  # Indicar la especie de interés. En este caso, Eucalyptus globulus tiene el código 61 en el IFN3.
  # Para Pinus pinaster, se usaría el IRI correspondiente a Species28.
  VALUES ?sp {<https://datos.iepnb.es/kos/sector-publico/medio-ambiente/ifn3/taxon/Species61>}

  # Selecciona parcelas del IFN3 que contienen la especie especificada y tienen posición y provincia definida.
  ?plot3 a <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/Plot> ;
         <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/containsSpecies> ?existencias ;
         <http://crossforest.eu/position/ontology/hasPosition> ?pos ;
         <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/province> <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn3/province/province33> .

  # Obtiene las coordenadas geográficas (latitud y longitud) de las parcelas.
  ?pos <http://crossforest.eu/position/ontology/hasCoordinateReferenceSystem> <http://epsg.w3id.org/data/coordinateReferenceSystem/crs4326> ;
       <http://epsg.w3id.org/ontology/coordinateAxis/axis106> ?lat ;
       <http://epsg.w3id.org/ontology/coordinateAxis/axis107> ?lng .

  # Recupera las clases diamétricas (?DC) asociadas a la especie de interés en las parcelas.
  ?existencias <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/hasSpeciesByDiametricClass> ?DC ;
               <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/hasSpecies> ?sp .
}
# Agrupa los resultados por parcela y sus coordenadas geográficas.
GROUP BY ?plot3 ?lat ?lng 

# Filtra las parcelas con más de dos clases diamétricas asociadas a la especie.
HAVING (COUNT(?DC) > 2)
