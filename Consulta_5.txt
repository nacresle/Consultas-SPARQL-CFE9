# Consulta 5. Consulta para obtener parcelas mixtas en Asturias (33), donde ninguna especie domina el 90% o más del área basal,
# pero al menos una especie tiene un porcentaje de área basal entre el 15% y el 90%.


SELECT DISTINCT ?plot3 ?lat ?lng ?nameSp (100*?spBasalArea)/SUM(?G) AS ?PorG
WHERE {
  # Selecciona parcelas de IFN3 que contienen especies y tienen información de posición.
  ?plot3 a <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/Plot> ;
         <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/containsSpecies> ?existencias ;
         <http://crossforest.eu/position/ontology/hasPosition> ?pos.

  # Obtiene las coordenadas geográficas (latitud y longitud) de las parcelas.
  ?pos <http://crossforest.eu/position/ontology/hasCoordinateReferenceSystem> <http://epsg.w3id.org/data/coordinateReferenceSystem/crs4326> ;
       <http://epsg.w3id.org/ontology/coordinateAxis/axis106> ?lat ;
       <http://epsg.w3id.org/ontology/coordinateAxis/axis107> ?lng .

  # Recupera el área basal total de las especies en cada parcela.
  ?existencias <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/hasBasalAreaInM2byHA> ?G .

  # Subconsulta: Selecciona parcelas dentro de una provincia específica (province33) 
  # y calcula el área basal por especie en esas parcelas.
  {
     SELECT ?plot3 ?nameSp ?spBasalArea 
     WHERE {
         # Filtra las parcelas por provincia (province33) y obtiene las especies contenidas.
         ?plot3 a <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/Plot> ;
                <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/province> <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn3/province/province33>;
                <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/containsSpecies> ?existencias .

         # Recupera información sobre las especies y sus áreas basales específicas.
         ?existencias <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/hasSpecies> ?sp ;
                      <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/hasBasalAreaInM2byHA> ?spBasalArea .

         # Obtiene el nombre aceptado de cada especie sin incluir al autor.
         ?sp <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn3/acceptedNameWithoutAuthor> ?nameSp .
     }
  }
}
# Agrupa los resultados por parcela, coordenadas, nombre de especie y área basal de la especie.
GROUP BY ?plot3 ?lat ?lng ?nameSp ?spBasalArea

# Filtra las parcelas donde el porcentaje de área basal de una especie está entre 15% y 90%.
HAVING ((100*?spBasalArea)/SUM(?G) < 90 && (100*?spBasalArea)/SUM(?G) >= 15)

# Ordena los resultados por parcela.
ORDER BY ?plot3
